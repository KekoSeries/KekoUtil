buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'kr.entree.spigradle' version '2.1.1'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'io.codearte.nexus-staging' version '0.21.2'
}

apply plugin: 'com.bmuschko.nexus'

compileJava {
    options.encoding = 'UTF-8'
}

javadoc {
    options.encoding = 'UTF-8'
}

group = "tr.com.infumia"
version = this.kekoutilVersion

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    jcenter()
    spigot()
    jitpack()
    maven {
        url = "https://repo.aikar.co/content/groups/aikar/"
    }
    maven {
        url = "https://dl.bintray.com/portlek/maven"
    }
    maven {
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        url = "https://repo.codemc.org/repository/maven-public"
    }
}

dependencies {
    // Tools
    implementation("io.github.portlek:SmartInventory:2.5.3")
    implementation("com.github.cryptomorin:XSeries:7.1.3")
    implementation("io.github.portlek:BukkitItemBuilder:1.7.4")
    implementation("io.github.portlek:bukkit-location:1.2")
    implementation("io.github.portlek:bukkit-version:1.3")
    implementation("io.github.portlek:version-matched:2.6")
    implementation("io.github.portlek:mapentry:1.2")
    implementation("io.github.portlek:replaceable:1.2")
    implementation("io.github.portlek:reflection:3.2")
    implementation("io.github.portlek:version:1.2")
    implementation("me.carleslc.Simple-YAML:Simple-Configuration:1.7.1")
    implementation("me.carleslc.Simple-YAML:Simple-Yaml:1.7.1")
    implementation("io.github.portlek:jsongration:1.3")
    implementation("com.github.WesJD.AnvilGUI:anvilgui:master-SNAPSHOT")
    implementation("io.github.bananapuncher714:NBTEditor:7.16")
    implementation("com.zaxxer:HikariCP:3.4.5")

    // Other Stuffs
    compileOnly("io.github.portlek:configs-core:4.9.1")
    compileOnly("io.github.portlek:configs-bukkit:4.9.1")
    compileOnly("org.jetbrains:annotations:20.0.0")
    compileOnly("org.projectlombok:lombok:1.18.12")
    annotationProcessor("org.projectlombok:lombok:1.18.12")

    // Main
    compileOnly spigot()

    // Hooks
    compileOnly("me.clip:placeholderapi:2.10.6")
    compileOnly(vault()) {
        exclude group: 'org.bukkit'
    }
    compileOnly luckPerms()
}

shadowJar {
    setArchivesBaseName(this.name)
    archiveClassifier.set("")
    archiveVersion.set("")
}

spigot {
    description = "A library that provides to write easy and clean code for Bukkit plugins."
    apiVersion = "1.13"
    authors = ["infumia"]
    softDepends = ["Vault", "LuckPerms", "PlaceholderAPI"]
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}

nexusStaging {
    packageGroup = this.group
}

task gradleVersion(type: Wrapper) {
    gradleVersion = '6.5.1'
}

modifyPom {
    project {
        name 'kekoutil'
        description 'A library that provides to write easy and clean code for Bukkit plugins.'
        url 'https://github.com/KekoSeries/KekoUtil'

        scm {
            connection 'scm:git:git://github.com/KekoSeries/KekoUtil.git'
            developerConnection 'scm:git:ssh://github.com/KekoSeries/KekoUtil.git'
            url 'https://github.com/KekoSeries/KekoUtil'
        }
        licenses {
            license {
                name 'MIT License'
                url 'https://opensource.org/licenses/mit-license.php'
            }
        }
        developers {
            developer {
                id 'portlek'
                name 'Hasan Demirta≈ü'
                email 'hasan@infumia.com.tr'
                roles {
                    role 'Architect'
                    role 'Developer'
                }
                timezone '+3'
            }
        }
    }
}